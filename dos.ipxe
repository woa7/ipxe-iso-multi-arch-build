#!ipxe

:dos_menu
menu FreeDOS
item --key i install FreeDOS 1.1 src (installation iso) [i]
item --key l live FreeDOS 1.1 Live [l]
item --key c custom_iso Boot custom CDROM ISO image [c]
item --key d custom_img Boot custom FD/HDD image [d]
item --key u ubcd UBCD - Ultimate boot CD [u]
choose value || goto dos_menu
goto boot_dos_${value}

:boot_dos_install
set dir ${mirror}/mirrors/freedos/1.1
kernel ${url}memdisk iso ${params} || goto die
initrd ${dir}/fd11src.iso || goto die
boot

:boot_dos_live
set dir ${mirror}/mirrors/freedos/1.1
kernel ${url}memdisk raw keeppxe ${params} || goto die
initrd ${dir}/fd11live.img.zip || goto die
boot

:boot_dos_custom_iso
kernel ${url}memdisk iso ${params} || goto die
goto enter_path

:boot_dos_custom_img
kernel ${url}memdisk ${params} || goto die
goto enter_path

:enter_path
echo -n Enter image URL: ${}
read imgurl
isset ${imgurl} || goto dos_menu
initrd ${imgurl} || goto enter_path
boot

:boot_dos_ubcd
kernel ${url}memdisk iso keeppxe ${params} || goto die
initrd http://www.salstar.sk/pxe/test/ubcd511.iso || goto die
boot
