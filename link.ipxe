#!ipxe

set esc:hex 1b
set term_color:hex 1b:5b:31:3b
set c_red ${term_color:string}31m
set c_green ${term_color:string}32m
set c_blue ${term_color:string}34m
set c_reset ${esc:string}[0m
echo ${c_reset}
echo ${c_green}boot.salstar.sk${c_reset} - ${c_red}SAL's ${c_blue}boot menu${c_reset}
echo ${c_reset}

:start
set boot_salstar_version 1

:link_retry
# try 2-times
iflinkwait --timeout=60000 || echo Link wait failed!
ifconf || ifconf || goto link_menu

:boot
set base https://boot.salstar.sk/
set base_ip http://158.197.16.66/boot/

# show address
echo IP: ${ip}, netmask: ${netmask}, gateway: ${gateway}

# try to sync date
ntp ntp.salstar.sk ||

:boot_chain
# allow loading of custom script set via DHCP server
# option site-local-128 code 128 = string;
# option site-local-128 "http://host.domain/filename";
isset ${128:string} && chain --autofree ${128:string} ||
chain --autofree ${base}by_mac/${mac} ||
chain --autofree ${base}by_ip/${ip} ||
chain --autofree --replace ${base}menu.ipxe && exit ||

:link_menu
prompt --timeout 20000 Press any key to enter failsafe menu ... || exit
menu Internal script failed!
item --key l exit Boot local hdd [l]
item --key n network Manual network configuration [n]
item --key v vlan Configure VLANs [v]
item --key c chain Chainload another iPXE [c]
item --key r retry Retry [r]
item --key 4 retry4 Retry using IPv4 protocol only [4]
item --key b reboot Reboot [b]
item --key w reboot_warm Warm reboot [w]
item --key f boot_ip Fallback boot without DNS [f]
item --key s shell Start iPXE shell [s]
choose --timeout 60000 link_menu || exit
goto link_${link_menu}

:link_exit
exit

:link_network
echo Network configuration:
echo -n Enter network card number [0 for net0, defaults to 0]: ${}
read net
isset ${net} || set net 0
echo -n Enter IP address: ${}
read net${net}/ip
echo -n Enter netmask: ${}
read mask
iseq ${mask} 8 && set mask 255.0.0.0 ||
iseq ${mask} 9 && set mask 255.128.0.0 ||
iseq ${mask} 10 && set mask 255.192.0.0 ||
iseq ${mask} 11 && set mask 255.224.0.0 ||
iseq ${mask} 12 && set mask 255.240.0.0 ||
iseq ${mask} 13 && set mask 255.248.0.0 ||
iseq ${mask} 14 && set mask 255.252.0.0 ||
iseq ${mask} 15 && set mask 255.254.0.0 ||
iseq ${mask} 16 && set mask 255.255.0.0 ||
iseq ${mask} 17 && set mask 255.255.128.0 ||
iseq ${mask} 18 && set mask 255.255.192.0 ||
iseq ${mask} 19 && set mask 255.255.224.0 ||
iseq ${mask} 20 && set mask 255.255.240.0 ||
iseq ${mask} 21 && set mask 255.255.248.0 ||
iseq ${mask} 22 && set mask 255.255.252.0 ||
iseq ${mask} 23 && set mask 255.255.254.0 ||
iseq ${mask} 24 && set mask 255.255.255.0 ||
iseq ${mask} 25 && set mask 255.255.255.128 ||
iseq ${mask} 26 && set mask 255.255.255.192 ||
iseq ${mask} 27 && set mask 255.255.255.224 ||
iseq ${mask} 28 && set mask 255.255.255.240 ||
iseq ${mask} 29 && set mask 255.255.255.248 ||
set net${net}/netmask ${mask}
echo -n Enter gateway: ${}
read net${net}/gateway
echo -n Enter DNS server: ${}
read net${net}/dns
ifopen net${net}
goto boot

:link_vlan
echo -n Enter network card number [0 for net0, defaults to 0]: ${}
read net
isset ${net} || set net 0
echo -n Enter VLAN number: ${}
read vlan
vcreate --tag ${vlan} net${net}
iflinkwait --timeout=60000 || echo Link wait failed!
ifconf net${net}-${vlan}
goto boot

:link_retry4
iflinkwait --timeout=60000 || echo Link wait failed!
ifconf --configurator dhcp || goto link_menu
goto boot

:link_shell
shell
goto start

:link_chain
iflinkwait --timeout=60000 || echo Link wait failed!
ifconf
chain http://boot.salstar.sk/ipxe/ipxe.lkrn && exit ||
goto link_menu

:link_reboot_warm
reboot --warm ||

:link_reboot
reboot ||
goto link_menu

:link_boot_ip
chain --autofree --replace ${base_ip}menu.ipxe ||
exit

:tftp
set next-server 158.197.16.70
clear base
echo Trying to boot from tftp server at ${next-server} ...
goto boot_chain
