#!ipxe

:start
dhcp || goto link_menu

:boot
set base https://pxe.salstar.sk/
set base_ip http://158.197.16.66/pxe/

:boot_chain
chain ${base}by_mac/${mac} ||
chain ${base}by_ip/${ip} ||
chain ${base}script.ipxe && exit ||

:link_menu
prompt --timeout 20000 Press any key to enter failsafe menu ... || exit
menu Internal script failed!
item --key l exit Boot local hdd [l]
item --key v vlan Configure VLANs [v]
item --key f boot_ip Fallback boot without DNS [f]
item --key s shell Start iPXE shell [s]
choose link_menu || exit
goto link_${link_menu}

:link_exit
exit

:link_vlan
echo -n Enter VLAN number: ${}
read vlan
vcreate --tag ${vlan} net0
dhcp net0-${vlan}
goto boot

:link_shell
shell
goto start

:link_boot_ip
chain ${base_ip}script.ipxe ||
exit

:tftp
set next-server 158.197.16.70
clear base
echo Trying to boot from tftp server at ${next-server} ...
goto boot_chain
